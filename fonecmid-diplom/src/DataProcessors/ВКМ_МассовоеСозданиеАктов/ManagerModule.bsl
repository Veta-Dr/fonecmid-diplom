#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьСписокРеализаций(НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВКМ_ОбслуживаниеКлиента.Договор КАК Договор
		|ПОМЕСТИТЬ ВТ_СписокДоговоров
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО ВКМ_ОбслуживаниеКлиента.Договор = ДоговорыКонтрагентов.Ссылка
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
		|	И ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора < &КонецПериода
		|	И ДоговорыКонтрагентов.ВКМ_КонецДействияДоговора > &НачалоПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_СписокРеализаций
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РеализацияТоваровУслуг.Договор В
		|			(ВЫБРАТЬ
		|				ВТ_СписокДоговоров.Договор КАК Договор
		|			ИЗ
		|				ВТ_СписокДоговоров КАК ВТ_СписокДоговоров)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписокДоговоров.Договор КАК Договор,
		|	ЕСТЬNULL(ВТ_СписокРеализаций.Ссылка, """") КАК Реализация,
		|	ВТ_СписокДоговоров.Договор.Владелец КАК Клиент,
		|	ВТ_СписокДоговоров.Договор.Организация КАК Организация
		|ИЗ
		|	ВТ_СписокДоговоров КАК ВТ_СписокДоговоров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокРеализаций КАК ВТ_СписокРеализаций
		|		ПО ВТ_СписокДоговоров.Договор = ВТ_СписокРеализаций.Ссылка.Договор";
	
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ВсегоДоговоров = РезультатЗапроса.Выбрать().Количество();
	Счетчик = 0;
                                            
	СписокРеализаций = Новый Массив;
	
	Пока Выборка.Следующий() Цикл 
		
		Счетчик = Счетчик + 1;
		ДлительныеОперации.СообщитьПрогресс(Счетчик * 100 / ВсегоДоговоров, "Обработка договоров...");

		Если ЗначениеЗаполнено(Выборка.Реализация) Тогда
			// создавать реализацию не нужно
			СписокРеализаций.Добавить(Новый Структура("Договор, Реализация", Выборка.Договор, Выборка.Реализация));
			
		Иначе
			// нужно создать реализацию
		    НоваяРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент(); 
				
			НоваяРеализация.Дата = ТекущаяДата();
			НоваяРеализация.Ответственный = Пользователи.ТекущийПользователь(); 
			НоваяРеализация.Контрагент = Выборка.Клиент;
			НоваяРеализация.Организация = Выборка.Организация;
			НоваяРеализация.Договор = Выборка.Договор;
			
			НоваяРеализация.ВКМ_ВыполнитьАвтозаполнение(Выборка.Договор);
			
			Если НоваяРеализация.ПроверитьЗаполнение() Тогда        
				НоваяРеализация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			КонецЕсли;
			
			СписокРеализаций.Добавить(Новый Структура("Договор, Реализация", Выборка.Договор, НоваяРеализация.Ссылка));
			
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат СписокРеализаций;	
	
КонецФункции


#КонецОбласти 
#КонецЕсли