

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ДлительнаяОперация = НачатьВыполнениеНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьДанные", ЭтотОбъект); 
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
    ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;

    ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания); 

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьВыполнениеНаСервере()
	
	ИмяФункции = "Обработки.ВКМ_МассовоеСозданиеАктов.ПодготовитьСписокРеализаций";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции, Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьДанные(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		
		Возврат; 
		
	КонецЕсли;   
	
	СписокРеализаций = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ЗаполнитьСписокРеализаций(СписокРеализаций);
	
КонецПроцедуры  

&НаКлиенте
Процедура ЗаполнитьСписокРеализаций(СписокРеализаций) 
	
	Объект.Список.Очистить();
	
	Для Каждого Реализация Из СписокРеализаций Цикл
		
		НоваяСтрока = Объект.Список.Добавить(); 
		НоваяСтрока.Договор = Реализация.Договор; 
		НоваяСтрока.Реализация = Реализация.Реализация; 
		
	КонецЦикла;

КонецПроцедуры


#КонецОбласти