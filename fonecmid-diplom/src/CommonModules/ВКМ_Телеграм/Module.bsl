#Область СлужебныеПроцедурыИФункции

Функция ОтправитьСообщение(ТекстСообщения) 
	
	ТокенБота = Константы.ВКМ_ТокенУправленияТелеграмБот.Получить();
	
	Если  ПустаяСтрока(ТокенБота) Тогда
		ЗаписьЖурналаРегистрации("Телеграм.ОшибкаОтправки", УровеньЖурналаРегистрации.Ошибка,,,"Сообщение не отправлено, не заполнен токен бота");
		Возврат Ложь;
	КонецЕсли;
	
	ИдентификаторГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	Соединение = Новый HTTPСоединение("api.telegram.org",443,,,,,Новый ЗащищенноеСоединениеOpenSSL());	
	ТекстЗапроса = "/bot" + ТокенБота + "/sendMessage?chat_id=" + ИдентификаторГруппы;	

	Данные = Новый Структура;
	Данные.Вставить("text", ТекстСообщения);
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	СтрокаJSON = ЗаписьJSON.Закрыть();

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json");

	Запрос = Новый HTTPЗапрос(ТекстЗапроса, Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
	
	Ответ = Соединение.Получить(Запрос);
		
	Если Ответ.КодСостояния = 200 Тогда
		ЗаписьЖурналаРегистрации("Телеграм.Сообщение отправлено", УровеньЖурналаРегистрации.Информация,,,"Сообщение отправлено успешно");
		Возврат Истина;
	Иначе
		ЗаписьЖурналаРегистрации("Телеграм.ОшибкаОтправки", УровеньЖурналаРегистрации.Ошибка,,,"Сообщение не отправлено"); 
		Возврат Ложь;	
	КонецЕсли;

КонецФункции

Процедура ВКМ_ОтправкаУведомленийВЧатБот() Экспорт
	
	Выборка = Справочники.ВКМ_УведомленияТелеграмБоту.Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = Выборка.ТекстСообщения; 
		Если ОтправитьСообщение(ТекстСообщения) Тогда
			Выборка.ПолучитьОбъект().Удалить();
		КонецЕсли;
	КонецЦикла;  
	
КонецПроцедуры

#КонецОбласти